buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.0-SNAPSHOT'
    }
}

apply plugin: 'forge'

ext.buildProps = file "build.properties"

buildProps.withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

version = "${config.version.mod.major}.${config.version.mod.minor}.${config.version.mod.revis}"
group = "icbm"
archivesBaseName = "ICBM"
sourceCompatibility = 1.7
targetCompatibility = 1.7

minecraft {
    version = "${config.version.minecraft}-${config.version.forge}"
	
	replaceIn "Reference.java"
	replace "@MAJOR@", config.version.mod.major
	replace "@MINOR@", config.version.mod.minor
	replace "@REVIS@", config.version.mod.revis
	replace "@BUILD@", System.getenv("BUILD_NUMBER")
}

if (System.getenv("BUILD_NUMBER") != null)
    version += ".${System.getenv("BUILD_NUMBER")}"


processResources {
	from 'build.properties'
}

task copyBuildXml(type: Copy) {
    from 'build.properties'
    into 'output'
}

task apiZip(type: Zip) {
    classifier = 'api'
    from sourceSets*.allSource
    include 'icbm/api/**'
    destinationDir = file 'output'
}

artifacts {
    archives apiZip
}

jar {
    dependsOn copyBuildXml
    classifier = ''
    destinationDir = file 'output'
}

repositories {
    maven { url 'file://var/www/maven/' }
	
	ivy {
			name 'CB FS'
			artifactPattern "http://files.minecraftforge.net/[module]/[module]-dev-[revision].[ext]"
	}
}
dependencies {
    compile 'universalelectricity:Universal-Electricity:3.0.+:dev'
    compile 'calclaviacore:calclavia-core:1.1.+:dev'
	compile name: 'CodeChickenLib', version: "1.6.4-1.0.0.49", ext: 'jar'
	compile name: 'ForgeMultipart', version: "1.6.4-1.0.0.233", ext: 'jar'
}
